#!/usr/bin/env lsc

require! {
  commander
  \../src/netns
  phantasm
}

commander
  .usage '[options]'
  .option '-i, --ip <ip>' 'ip to create namespace'
  .parse process.argv

# sanity checks
ip-re = /^(?:(?:25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)\.){3}(?:25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)$/
unless commander.ip and commander.ip.match ip-re
  console.error 'invalid ip address'
  process.exit 1

function run-in-phantomjs
  document.body.innerText

function run-test ns-exec, cb
  (page, phantomjs) <- phantasm.create ns-exec
  phantomjs.process.on \exit, ->
    console.error '`-phantomjs.exit'
    cb void
  (status) <- page.open 'http://foo.com'
  if status is \success
    (err, results) <- phantasm.series page, [{fn: run-in-phantomjs}]
    if err
      console.error err
    else
      console.log results
    phantomjs.exit!
  else
    phantomjs.exit!

ns = new netns commander.ip
(err) <- ns.create
if err
  console.error err
  process.exit 1
console.log '`-create'
(err) <- ns.test
if err
  console.error err
  process.exit 1
console.log '`-test'
<- run-test { binary: "ip netns exec #{ns.name} phantomjs" }
(err) <- ns.delete
if err
  console.error err
  process.exit 1
console.log '`-delete'

# vim:ft=ls fdm=indent

#!/usr/bin/env lsc

require! {
  commander
  \../lib/netns
}

commander
  .usage '[options] <command>'
  .option '-i, --ip <ip>' 'ip to create namespace'
  .option '-p, --prerouting' 'set up prerouting, allowing inbound traffic to the namespace, defaults to false'
  .parse process.argv

# sanity checks
ip-re = /^(?:(?:25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)\.){3}(?:25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)$/
unless commander.ip and commander.ip.match ip-re
  console.error 'invalid ip address'
  process.exit 1
unless commander.args.length
  console.error 'missing command'
  process.exit 1

ns = new netns commander.ip commander.prerouting
(err, ns-proc) <- ns.run commander.args.join(' '), _, { -verify, +cleanup }
if err
  console.error err
  process.exit 1
ns-proc.stdout.on \data, (-> process.stdout.write it)
ns-proc.stderr.on \data, (-> process.stderr.write it)

# vim:ft=ls fdm=indent
